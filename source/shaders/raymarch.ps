struct ps_in
{
	float4		Position : SV_Position;
};

cbuffer raymarch_params : register(b0)
{
	uint		ScreenWidth,
				ScreenHeight;
	float		MinVal,
				MaxVal;
	float3		LightPos;
};

static const uint MaxIterations = 32;

Texture3D<float>		Volume : register(t0);
Texture2D<float4>		BackPositions : register(t1);  // backface-culled, so frontface
Texture2D<float4>		FrontPositions : register(t2); // frontface-culled, so backface
Texture1D<float4>		Colormap : register(t3);
SamplerState			LinearSampler : register(s0);

float4		Accumulate(float4 Color, float4 NewColor, float Brightness);
float4		CastRay(float3 RayOrigin, float3 RayDirection, float tMin, float tMax, float dt);
float4		CastRayMIP(float3 RayOrigin, float3 RayDirection, float tMin, float tMax, float dt);

float4
main(ps_in Input) : SV_Target
{
	float2		Tex;
	float3		PosFront,
				PosBack,
				Dir;

	Tex = Input.Position.xy / float2(ScreenWidth, ScreenHeight);

	PosBack = BackPositions.Sample(LinearSampler, Tex).xyz;
	PosFront = FrontPositions.Sample(LinearSampler, Tex).xyz;

	Dir = normalize(PosBack - PosFront);

	float tMin = 0;
	float tMax = length(PosFront - PosBack);
	float dt = length(PosFront - PosBack) / MaxIterations;

	float4 Color = CastRay(PosFront, Dir, tMin, tMax, dt);

	return (Color);
}

float4
CastRay(float3 RayOrigin,
		float3 RayDirection,
		float tMin,
		float tMax,
		float dt)
{
	float t = tMin;
	float4 Color = float4(0, 0, 0, 0);

	[loop]
	while (t < tMax)
	{
		float3 Pos = RayOrigin + t * RayDirection;
		float Density = Volume.Sample(LinearSampler, Pos, int3(0, 0, 0));
		float NormalizedDensity = (Density - MinVal) / (MaxVal - MinVal);

		float4 SampledColor = float4(Colormap.Sample(LinearSampler, NormalizedDensity).rgb, 10 * dt);

		Color = Accumulate(Color, SampledColor, 1);

		t += dt;
	}

	return (Color);
}

float4
CastRayMIP(float3 RayOrigin,
		   float3 RayDirection,
		   float tMin,
		   float tMax,
		   float dt)
{
	float t = tMin;
	float MaxDensity = -1000;

	[loop]
	while (t < tMax)
	{
		float3 Pos = RayOrigin + t * RayDirection;
		float Density = Volume.Sample(LinearSampler, Pos, int3(0, 0, 0));
		float NormalizedDensity = (Density - MinVal) / (MaxVal - MinVal);

		if (NormalizedDensity > MaxDensity)
		{
			MaxDensity = NormalizedDensity;
		}

		t += dt;
	}

	float3 SampledColor = Colormap.Sample(LinearSampler, MaxDensity).rgb;
	float4 Color = float4(SampledColor, 1.0);

	return (Color);
}

float4
Accumulate(float4 Color,
		   float4 NewColor,
		   float Brightness)
{
	float OldAlpha = 1.0 - Color.a;
	float NewAlpha = min(1.0, NewColor.a * Brightness);

	return (Color + float4(NewColor.rgb, 1.0) * OldAlpha * NewAlpha);
}

